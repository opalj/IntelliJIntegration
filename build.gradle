plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.3.12'

    id 'checkstyle'
    id 'pmd'
    id "com.github.spotbugs" version "1.6.6"
    id 'com.github.sherter.google-java-format' version '0.8'
}

group '23'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'de.opal-project:abstract-interpretation-framework_2.12:2.0.1'
    compile 'de.opal-project:bytecode-disassembler_2.12:2.0.1:'
}

//runIde {
//    dependsOn 'check'
//}

// build.dependsOn(check), which means that 'check' will be executed whenever 'build' will be executed
check {
    // googleJavaFormat will report "NO-SOURCE" if everything is properly formatted (-> not harmful in any way, can ignore)
    dependsOn 'googleJavaFormat'

    // dependsOn [checkstyle, pmd and spotbugsMain] per default

    // TODO maybe spotbugsMain as first task to be executed (if fails -> no time wasted with the rest)
    tasks.findByName('checkstyleMain').mustRunAfter('googleJavaFormat')
    tasks.findByName('pmdMain').mustRunAfter('checkstyleMain')
    tasks.findByName('spotbugsMain').mustRunAfter('pmdMain')
}

intellij {
    version '2018.2.2'
}
patchPluginXml {
    changeNotes """
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""
}


tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }

    // TODO: remove later
    ignoreFailures = true
}

checkstyle {
    toolVersion "8.10.1"
//    configFile = file("${rootDir}/config/checkstyle/ckeckstyle.xml")
    config = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml')

    maxErrors 2
}

